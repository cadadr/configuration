#!/usr/bin/env perl
# git-clonex --- clone an external git repo the way I like

use v5.24;

use strict;
use warnings;

use File::Temp qw(tempdir);
use Getopt::Long;
use IPC::Run3 qw(run3);

my $tmp  = 0;
my $yank = 0;

GetOptions ("temporary|tmp" => \$tmp,
	    "yank|y"        => \$yank,)
    or die "Command line error: $!";

my $repo_url = shift @ARGV or die "usage: git clonex [-ty] GITHUB-URL";

unless ($repo_url =~ /
           https? :\/\/ (www\.)?
           (?<forge>[^\.]+)
           \. [^\/]+ \/
           (?<user>[\w\d\-_]+) \/
           (?<repo>[\w\d\-_]+) \/?
       /x )
   {
    die "Bad URL: $repo_url";
} else {
    my $local_path;
    if ($tmp) {
	my $dirname = "$+{forge}-$+{user}-$+{repo}";
	$local_path = tempdir ("$dirname-XXXXXXXX", TMPDIR => 1);
    } else {
	$local_path = "$ENV{'HOME'}/co/External/$+{forge}-$+{user}-$+{repo}";
    }
    system ("git-clone", $repo_url, $local_path) == 0
	or die "git error: $!";
    say "\n    Type this to go to the checkout:";
    say "      \$ cd $local_path";
    if ($yank) {
	my @cmd = ("xclip", "-i");
	run3 (\@cmd, \$local_path) or die "xclip: $!";
	say "\nYanked checkout path as selection!";
    }
}

=pod

=encoding utf8

=head1 git-clonex --- clone an external git repo the way I like

=head2 Synopsis

=over

git clonex [-ty] REPO-URL

=back

=head2 Flags

=over

    -t|--temporary

=over

    Make a checkout in under the system's temporary directory.

=back

    -y|--yank

=over

    Copy the checkout path into the clipboard and selection if possible.

=back

=back

=head2 Arguments

=over

    REPO-URL

=over

    The forge URL for the repository.

=back

=back

=head2 Description

Clone a repo from a git forge to an appropriate location with an
appropriate name.  Appropriate as in according to my own
conventions.

This works for git forge URLs in the common format

=over

domain.tld/user/repo

=back

used by most common git forges, like Github, Gitlab, etc.

=cut
